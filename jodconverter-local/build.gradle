import org.apache.tools.ant.taskdefs.condition.Os
import org.jodconverter.Deps

ext.moduleName = 'JODConverter Local'
ext.moduleDescription = 'Module required in order to process local conversions for the Java OpenDocument Converter (JODConverter) project.'

dependencies {
    api project(":jodconverter-core")

    // We cannot use LibreOffice libraries by default.
    // See:
    // https://github.com/jodconverter/jodconverter/issues/113
    if (project.hasProperty("useLibreOffice")) {
        api Deps.libreoffice
    } else {
        api Deps.ooJuh
        api Deps.ooJurt
        api Deps.ooRidl
        api Deps.ooUnoil
    }

    implementation Deps.slf4jApi

    testImplementation Deps.slf4jLog4j
    testImplementation Deps.mockitoInline
    testImplementation Deps.springTest
    testImplementation project(path: ":jodconverter-core", configuration: "tests")
}

integrationTest {
    filter {
        excludeTestsMatching "*.Stress*"
        excludeTestsMatching "*.Performance*"
        excludeTestsMatching "*.Sandbox*"

        // Exclude filter tests on macos. LibreOffice seems to crash frequently on macos. See:
        // https://ask.libreoffice.org/t/libreoffice-crashing-on-change-window-focus-macos-ventura/83415
        // https://ask.libreoffice.org/t/on-mac-libreoffice-keeps-crashing/74751/36?page=2
        //
        // Ignore those tests for now until LO is more stable on macos.
        if (Os.isFamily(Os.FAMILY_MAC)) {
            excludeTestsMatching "org.jodconverter.local.filter.*"
        }
    }
}
