import org.jodconverter.Deps

ext.moduleName = 'JODConverter Local'
ext.moduleDescription = 'Module required in order to process local conversions for the Java OpenDocument Converter (JODConverter) project.'

dependencies {
    api project(":jodconverter-core")

    // We cannot use LibreOffice libraries by default.
    // See:
    // https://github.com/jodconverter/jodconverter/issues/113
    if (project.hasProperty("useLibreOffice")) {
        api Deps.libreoffice
    } else {
        api Deps.ooJuh
        api Deps.ooJurt
        api Deps.ooRidl
        api Deps.ooUnoil
    }

    implementation Deps.slf4jApi

    testImplementation Deps.slf4jLog4j
    testImplementation Deps.mockitoInline
    testImplementation Deps.springTest
    testImplementation project(path: ":jodconverter-core", configuration: "tests")
}

integTest {
    filter {
        excludeTest "org.jodconverter.local.StressITest"
        excludeTest "org.jodconverter.local.PerformanceITest"
        excludeTest "org.jodconverter.local.SandboxITest"

        // Macos build failure investigation
        excludeTestsMatching "org.jodconverter.local.filter.*"
    }
}
